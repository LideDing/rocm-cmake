
set(DIR ${CMAKE_ARGV3})

macro(test_expect_eq X Y)
    if(NOT ${X} EQUAL ${Y})
        message(FATAL_ERROR "EXPECT FAILURE: ${X} != ${Y} ${ARGN}")
    endif()
endmacro()

macro(test_expect_matches X Y)
    if(NOT ${X} MATCHES ${Y})
        message(FATAL_ERROR "EXPECT FAILURE: ${X} != ${Y} ${ARGN}")
    endif()
endmacro()

file(GLOB DEB_FILES ${DIR}/*.deb)

if(DEB_FILES)
    foreach(DEB ${DEB_FILES})
        execute_process(COMMAND dpkg -c ${DEB} OUTPUT_VARIABLE OUT)
        set(HAS_LIB 0)
        set(HAS_CMAKE 0)
        set(HAS_HEADER 0)
        if(OUT MATCHES "libsimple.a")
            set(HAS_LIB 1)
        endif()
        if(OUT MATCHES "simple.h")
            set(HAS_HEADER 1)
        endif()
        if(OUT MATCHES "simple-config.cmake")
            set(HAS_CMAKE 1)
        endif()
        message("Debian ${DEB} contents:")
        message("${OUT}")
        message("---------")
        if(HAS_LIB)
            test_expect_eq(HAS_CMAKE 0)
            test_expect_eq(HAS_HEADER 0)
            test_expect_matches(${DEB} runtime)
        endif()
        if(HAS_CMAKE OR HAS_HEADER)
            test_expect_eq(HAS_LIB 0)
            test_expect_eq(HAS_CMAKE 1)
            test_expect_eq(HAS_HEADER 1)
            test_expect_matches(${DEB} dev)
        endif()
    endforeach()
endif()

